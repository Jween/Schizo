plugins {
    id "com.jfrog.bintray" version "1.8.0"
}

apply plugin: 'java'
apply plugin: 'maven-publish'

sourceSets {
    main {
        java.srcDirs += "src/main/references" // 接口
    }
}

String prjName = 'processor'
String prjDesc = 'Schizo processor for the IPC library Schizo which is a simple IPC library for android.'
publishing {
    publications {
        maven(MavenPublication) {
            groupId schizo.group
            version schizo.version
            artifactId prjName

            from components.java

            pom.withXml {
                def root = asNode()
                root.appendNode('description', prjDesc)
                root.appendNode('name', "${schizo.group}:$prjName")

                root.children().last() + schizo.pomConfig
            }
        }
    }
}

bintray {
    user = schizo.bintray.user
    key = schizo.bintray.apiKey

    publications = ['maven']

    pkg {
        repo = 'maven'
        name = "${schizo.group}:$prjName"
        userOrg = schizo.bintray.user
        licenses = ['Apache-2.0']
        websiteUrl = schizo.siteUrl
        vcsUrl = schizo.gitUrl
        labels = ['schizo', prjName]
        version {
            name = schizo.version
            desc = prjDesc
            released  = new Date()
            vcsTag = "${schizo.version}"
            attributes = ['android-library': 'io.jween:io.jween.schizo:schizo']

            gpg {
                sign = true
                passphrase = schizo.bintray.passphrase
            }
        }
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.squareup:javapoet:1.8.0'
    implementation 'io.jween.schizo:annotation:0.1'
}

sourceCompatibility = "1.7"
targetCompatibility = "1.7"
